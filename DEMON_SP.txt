actor GauntletDemon : Demon 10008
{
	spawnid 112
	Health 180
	radius 30
	MissileType GauntletDemonBall
    DropItem Mana 180
    dropitem MeatSpawner 50
    dropitem MeatSpawner 50
	DropItem Warspirit 32
	Translation "16:47=48:79"
	states
	{
	Missile:
		SARG E 4 A_FaceTarget
		SARG F 10 A_MissileAttack
		SARG G 5	
		SARG E 4 A_FaceTarget
		SARG F 10 A_MissileAttack
		SARG G 5
		SARG E 4 A_FaceTarget 
		SARG F 10 A_SkullAttack 
		SARG G 5 A_Gravity
		Goto See
	Death:
		SARG A 0
		SARG I 5 A_Scream
		SARG J 5 A_NoBlocking
		SARG K 5
		SARG L 5
		SARG M 5
		SARG N -1
		stop
	}
}

ACTOR DeadGauntletDemon
{
  Translation "16:47=48:79"
  States
  {
  Spawn:
    SARG N -1
    Stop
  }
}

actor GauntletDemonBall : DoomImpBall
{
	Radius 6
	Height 8
	Speed 15
	Damage 5
	Health 20
	PROJECTILE
	RENDERSTYLE ADD
	ALPHA 0.7
	SeeSound "imp/attack"
	DeathSound "imp/shotx"
	MissileType DemonBallTrail
	MissileHeight 0
	Decal DoomImpScorch
	Damagetype Fire
	States
	{
	Spawn:
		BAL1 A 3 Bright A_MissileAttack
		BAL1 B 3 Bright A_MissileAttack
		Loop

	Death:
		BAL1 C 3 Bright  A_MissileAttack
		BAL1 D 3 Bright  
		BAL1 E 3 Bright
		Stop
	}
}

actor DemonBallTrail
{
PROJECTILE
RENDERSTYLE TRANSLUCENT
+NOCLIP
ALPHA 0.3
Scale 0.5
States
{
Spawn:
SMK1 J 3
SMK1 ABCDEFGHI 2 Bright
Stop
}
}

ACTOR BaronDemon 10100 //NIGHTMARE DEMON
{
	Health 200
	Radius 30
	Height 56
	Speed 12
	Mass 200
	PainChance 200
	RenderStyle TRANSLUCENT
	Alpha 0.9
	MONSTER
	+FLOORCLIP
	+LONGMELEERANGE
	SeeSound "nightmareDemon/sight"
	AttackSound "nightmareDemon/melee"
	PainSound "nightmareDemon/pain"
	DeathSound "nightmareDemon/death"
	ActiveSound "nightmareDemon/active"
    Obituary "%o was spooked by a Nightmare Demon."
    DropItem Mana 90
    dropitem MeatSpawner 50
    dropitem MeatSpawner 50
	DropItem WarSpirit 48
    DropItem HBSpawner 24
    DropItem ABSpawner 24
        States
        {
        Spawn:
        	SAR2 AB 10 A_look
        	Loop
        See:
            SAR2 A 0 A_JumpIfHealthLower(100,"Dash")
            SAR2 A 0 A_Jump(25,"Dash")
        	SAR2 AABBCCDD 2 A_Chase
        	Loop
        Dash:
        	SAR2 AA 1 A_Chase
	        TROO A 0 A_SpawnItem("BaronDemonA",0)
        	SAR2 BB 1 A_Chase
	        TROO A 0 A_SpawnItem("BaronDemonB",0)
        	SAR2 CC 1 A_Chase
	        TROO A 0 A_SpawnItem("BaronDemonC",0)
        	SAR2 DD 1 A_Chase
	        TROO A 0 A_SpawnItem("BaronDemonD",0)
        	Loop
        Melee:
        	SAR2 E 6 A_FaceTarget
        	SAR2 F 6 A_FaceTarget
        	SAR2 G 6 A_SargAttack
        	Goto See
        Missile:
        	SAR2 O 6 A_FaceTarget
        	SAR2 P 8 A_CustomMissile("NDemonBall", 24, 0, 0)
        	Goto See
        Pain:
        	SAR2 H 3
        	SAR2 H 3 A_Pain
        	Goto See
        Death:
        	SAR2 I 5
        	SAR2 J 5 A_Scream
        	SAR2 K 5
        	SAR2 L 5 A_NoBlocking
        	SAR2 M 5
        	SAR2 N -1
        	Stop
        Raise:
        	SAR2 MLKJI 5
        	Goto See
        }
}

ACTOR DeadBaronDemon
{
	RenderStyle TRANSLUCENT
	Alpha 0.9
	States
	{
	Spawn:
		SAR2 N -1
		Stop
  }
}

ACTOR BaronSpectre : BaronDemon
{
	Alpha 0.4
}

ACTOR BaronDemonA
{
	Radius 4
	Height 8
	Speed 0
	Damage 0
	Scale 0.6
	RenderStyle Translucent
	Alpha 0.4
	PROJECTILE
	+NOCLIP
   +NOTIMEFREEZE
	States
	{
	Spawn:
		SAR2 A 5 A_FaceTarget
		SAR2 A 1 A_FadeOut
		Wait
	}
}

ACTOR BaronDemonB
{
	Radius 4
	Height 8
	Speed 0
	Damage 0
	Scale 0.6
	RenderStyle Translucent
	Alpha 0.4
	PROJECTILE
	+NOCLIP
    +NOTIMEFREEZE
	States
	{
	Spawn:
		SAR2 B 5 A_FaceTarget
		SAR2 B 1 A_FadeOut
		Wait
	}
}


ACTOR BaronDemonC
{
	Radius 4
	Height 8
	Speed 0
	Damage 0
	Scale 0.6
	RenderStyle Translucent
	Alpha 0.4
	PROJECTILE
	+NOCLIP
   +NOTIMEFREEZE
	States
	{
	Spawn:
		SAR2 C 5 A_FaceTarget
		SAR2 C 1 A_FadeOut
		Wait
	}
}


ACTOR BaronDemonD
{
	Radius 4
	Height 8
	Speed 0
	Damage 0
	Scale 0.6
	RenderStyle Translucent
	Alpha 0.4
	PROJECTILE
	+NOCLIP
   +NOTIMEFREEZE
	States
	{
	Spawn:
		SAR2 D 5 A_FaceTarget
		SAR2 D 1 A_FadeOut
		Wait
	}
}


ACTOR NDemonBall //NIGHTMARE DEMON - Fireball
{
    Radius 8
    Height 6
    Speed 12
    Damage 4
    PROJECTILE
    SeeSound "64ball/shoot"
    DeathSound "64ball/explode"
    States
    {
    Spawn:
        DBAL ABABABABABABABABABABAB 2 Bright A_SpawnItemEx("NDemonFX",0,0,0,random(-1,1),random(-1,1),random(-1,1),0,128,0)
        Loop
    Death:
        DBAL CDEFGH 4 Bright
        Stop
    }
}

ACTOR NDemonFX
{   
   Radius 2
   Height 2
   +NOBLOCKMAP
   +NOGRAVITY
   +DONTSPLASH
   +FORCEXYBILLBOARD
   RENDERSTYLE ADD
   ALPHA 0.67
   Scale 0.5
   States
   {
   Spawn:
      NULL A 3 Bright
      DBAL CDEFGH 3 BRIGHT
      Stop 
   }
}

ACTOR CorpseWorm 30009
{
Health 150
Radius 32
Height 56
Speed 16
PainChance 100
MONSTER
+INVULNERABLE
+FLOAT
+NOGRAVITY
Scale 0.50
dropitem MeatSpawner 128
DropItem Warspirit 64
DropItem HBSpawner 24
DropItem HBSpawner 24
DropItem HBSpawner 24
States
{
Spawn:
CWRM L 10 A_Look
Loop
See:
CWRM LL 1 A_CustomMissile("CorpseGibs", 0, 0, 0)
CWRM FGHIJ 5
CWRM K 5 A_UnSetInvulnerable
CWRM ABCB 5 A_Chase
Goto See+8
Missile:
CWRM E 10 A_FaceTarget
CWRM EEE 8 BRIGHT A_CustomMissile("CorpseFX", 32, 0, 0)
Goto See+8
Pain:
CWRM D 10 A_Pain
Goto See+8
Death:
CWRM D 4 A_Scream
CWRM Z 4 A_NoBlocking
CWRM Z -1
Stop
}
}

ACTOR DeadCorpseWorm
{
  Scale 0.50
  States
  {
  Spawn:
    CWRM Z -1
    Stop
  }
}

ACTOR CorpseGibs : Peasant
{
Radius 1
Height 1
Speed 0
+NONSHOOTABLE+NOTELEPORT+NOCLIP+NOBLOCKMAP
SKIP_SUPER
Spawn Parent XDeath
}

ACTOR CorpseFX
{
Radius 4
Height 8
PROJECTILE
Speed 12
Damage 1
SeeSound "CorpseWormAttack"
DeathSound "CorpseWormHit"
Scale 0.10
States
{
Spawn:
CWFX A 16
CWFX AA 4 A_CustomMissile("Blood", 4, 0, 0)
Goto Spawn+1
Death:
BLUD ABC 5
Stop
}
}

ACTOR StealthLavaDemon 3128
{
   Health 150
   Speed 4
   Height 56
   Radius 30
   Mass 400
   PainChance 150
   MeleeDamage 4
   REACTIONTIME 8
   MONSTER
   +FLOORCLIP
   +NORADIUSDMG
   +STEALTH
   RenderStyle Translucent
   Alpha 0
   SeeSound "demon/sight"
   PainSound "demon/pain"
   DeathSound "demon/death"
   ActiveSound "demon/active"
   MeleeSound "demon/melee"
   damagefactor "fire",0
   damagefactor "ice",2.0
   DropItem WarSpirit 32
   dropitem Mana
   dropitem Mana 40
   DropItem WarSpirit 48
   DropItem DemonicHeart 16
   DropItem HBSpawner 24
   DropItem ABSpawner 24
   Obituary "An LavaDemon took out %o sorry ass."
   HitObituary "An LavaDemon took out %o sorry ass."
   States
   {
   Spawn:
      QWER AB 8 A_Look
      Loop
   See:
      QWER A 0 A_Jump(150,"see2")
	  QWER A 2 A_Chase
      QWER A 0 A_SetTranslucent(0.20, 0)  
	  QWER A 2 A_Chase
      QWER A 0 A_SetTranslucent(0.40, 0)  
	  QWER B 2 A_Chase
      QWER A 0 A_SetTranslucent(0.60, 0)  
	  QWER B 2 A_Chase
      QWER A 0 A_SetTranslucent(0.80, 0)  
	  QWER C 2 A_Chase
      QWER A 0 A_SetTranslucent(0.60, 0)  
	  QWER C 2 A_Chase 
      QWER A 0 A_SetTranslucent(0.40, 0)  
	  QWER D 2 A_Chase
      QWER A 0 A_SetTranslucent(0.20, 0)  
	  QWER D 2 A_Chase
      QWER A 0 A_SetSpeed(16, 0)
      QWER AABBCCDD 1 A_Chase
      QWER A 0 A_SetSpeed(4, 0)
      Loop
   See2:
      QWER AABBCCDD 2 A_Chase
      QWER H 0 A_CustomMissile("SZ_Hellfire", 0, 0, 0)
      Goto See+1
   Melee:
      QWER EF 8 A_FaceTarget
      QWER H 0 A_CustomMissile("SZ_Hellfire", 0, 0, 0)
      QWER H 0 A_CustomMissile("SZ_Hellfire", 32, -32, 0)
      QWER H 0 A_CustomMissile("SZ_Hellfire", -32, 32, 0)
      QWER G 8 A_MeleeAttack
      Goto See
   Pain:
      QWER H 2
      QWER H 2 A_Pain
      Goto See
   Death:
      QWER I 6
      QWER J 6 A_Scream
      QWER H 0 A_CustomMissile("SZ_Hellfire", 0, 24, 0)
      QWER K 4
      QWER L 4 A_NoBlocking
      QWER M 4
      QWER N -1
      Stop
   Raise:
      QWER NMLKJI 4
      Goto See
   }
}

ACTOR DeadLavaDemon
{
  States
  {
  Spawn:
    QWER N -1
    Stop
  }
}

ACTOR SZ_Hellfire2
{
     +NOCLIP
     +MISSILE
     +DROPOFF
     +RANDOMIZE
     +DONTSPLASH
     Scale 1.5
     Speed 0
     RENDERSTYLE ADD
     ALPHA 0.67
     DeathSound "vile/firecrkl"
     Damagetype Fire
     States
     {
     Spawn:
          NULL A 8
          FRTF ABC 1
          FRTF C 0 A_PlaySound("vile/firecrkl")
          FRTF C 0 A_Jump(200,4)
          FRTF C 0 A_CustomMissile("SZ_Hellfire2", 0, 0, 0)
          FRTF DEFGHIJKLMNO 1 Bright A_Explode(3,32)
          Stop
     }
}

//********************

ACTOR SZ_Hellfire1
{
     +NOCLIP
     +MISSILE
     +DROPOFF
     +DONTSPLASH
     Speed 2
     Damagetype Fire
     States
     {
     Spawn:
          NULL A 5 A_CustomMissile("SZ_Hellfire2", 0, 0, 0)
          NULL A 5 A_CustomMissile("SZ_Hellfire2", 0, 16, 0)
          NULL A 5 A_CustomMissile("SZ_Hellfire2", 0, -16, 0)
          NULL A 5 A_CustomMissile("SZ_Hellfire2", 0, 0, 0)
          NULL A 5 A_CustomMissile("SZ_Hellfire2", 0, 16, 0)
          NULL A 5 A_CustomMissile("SZ_Hellfire2", 0, -16, 0)
          Stop
     }
}

//********************

ACTOR SZ_Hellfire
{
     +NOBLOCKMAP
     +MISSILE
     +DROPOFF
     +DONTSPLASH
     Speed 5
     DeathSound "vile/firecrkl"
     Damagetype Fire
     States
     {
     Spawn:
          NULL A 1
          Loop
     Death:
          NULL A 0 Bright A_CustomMissile("SZ_Hellfire1", 0, 0, -180)
          NULL A 0 Bright A_CustomMissile("SZ_Hellfire1", 0, 16, -175)
          NULL A 0 Bright A_CustomMissile("SZ_Hellfire1", 0, 32, -170)
          NULL A 0 Bright A_CustomMissile("SZ_Hellfire1", 0, -16, -185)
          NULL A 0 Bright A_CustomMissile("SZ_Hellfire1", 0, -32, -190)
          Stop
     }
}

actor StoneDemon
{
  MONSTER
  +FLOORCLIP
  +NOBLOOD
  +DROPOFF
  +NORADIUSDMG
  Scale 1.1
  HitObituary "%o was smashed by a stone demon."
  Health 200
  PainChance 100
  Speed 8
  Radius 30
  Height 56
  Mass 9999999
  MissileType StoneDemonBoulder
  MissileHeight 24
  SeeSound "stone/sight"
  AttackSound "demon/melee"
  PainSound "demon/pain"
  DeathSound "stone/death"
  ActiveSound "demon/active"
  DropItem MeatSpawner 100
  DropItem Mana 128
  DropItem WarSpirit 28
  States
  {
  Spawn:
    STON AB 10 A_Look
    Loop
  See:
    STON AABBCCDD 2 A_Chase
    Loop
  Missile:
    STON EF 6 A_FaceTarget
    STON E 6 A_MissileAttack
    Goto See
  Melee:
    STON EF 8 A_FaceTarget
    STON G 0 A_SargAttack
    STON G 6 A_SargAttack
    STON EF 8 A_FaceTarget
    STON G 6 A_SargAttack
    Goto See
  Pain:
    STON H 3
    STON H 3 A_Pain
    Goto See
  Death:
    STON IIIIIIIIII 0 A_CustomMissile("StoneDemonMiniBoulder",16,0,random(0,359),2,random(0,40))
    STON I 5
    STON J 5 A_Scream
    STON K 5
    STON L 5 A_NoBlocking
    STON MN 5
    STON OP 4
    STON Q -1
    Stop
  Raise:
    STON NMLKJI 5
    Goto See
  }
}

ACTOR DeadStoneDemon
{
  Translation "16:47=48:79"
  States
  {
  Spawn:
    STON Q -1
    Stop
  }
}

actor StoneDemonStatue : SwitchableDecoration 8190
{
  +SOLID
  Scale 1.1
  Radius 30
  Height 56
  Mass 9999999
  States
  {
  Spawn:
  Inactive:
    STON A 10
    Loop
  Active:
    TNT1 A 1 A_SpawnItemEx("StoneDemon",0,0,0,0,0,0,0,32)
    Stop
  }
}

actor StoneDemonBoulder
{
  PROJECTILE
  -NOGRAVITY
  +DOOMBOUNCE
  +BOUNCEONACTORS
  SeeSound "stone/bounce"
  Radius 6
  Height 6
  Scale 0.7
  Speed 15
  VSpeed 2
  Damage 2
  States
  {
  Spawn:
    STNB ABCDEFGH 3
    Loop
  Death:
    STNB H 200
    Stop
  }
}

actor StoneDemonMiniBoulder : StoneDemonBoulder
{
  Damage 1
  Scale 0.35
  SeeSound ""
}

// SlimeWorm, a new monster by Captain toenail//

actor SlimeWorm
{
  obituary "%o was dissolved by a slimeworm."
  hitobituary "%o was digested by a slimeworm."
  health 250
  painchance 90
  speed 8
  radius 30
  height 56
  mass 400
  seesound "slimeworm/sight"
  attacksound "slimeworm/melee"
  painsound "slimeworm/pain"
  deathsound "slimeworm/death"
  activesound "slimeworm/active"
  bloodcolor yellow
  MONSTER
  +FLOORCLIP
  +MISSILEMORE
  +SHORTMISSILERANGE
  DropItem WarSpirit 28
  DropItem MeatSpawner 50
  DropItem DemonicHeart 16
  DropItem HBSpawner 24
  DropItem HBSpawner 24
  states
  {
  Spawn:
    WORM AB 10 A_Look
    loop
  See:
    WORM AABBCCDD 3 A_Chase
    loop
  Missile:
    WORM E 8 A_FaceTarget
    WORM F 8 A_PlaySound ("SlimeBall/Shoot")
    WORM F 0 A_CustomMissile ("SlimeBall1", 40, 0, random(-10,10), 2, random(10,20))
    WORM F 0 A_CustomMissile ("SlimeBall2", 40, 0, random(-10,10), 2, random(10,20))
    WORM F 0 A_CustomMissile ("SlimeBall3", 40, 0, random(-10,10), 2, random(10,20))
    WORM F 0 A_CustomMissile ("SlimeBall4", 40, 0, random(-10,10), 2, random(10,20))
    WORM F 0 A_CustomMissile ("SlimeBall5", 40, 0, random(-10,10), 2, random(10,20))
    WORM G 8
    goto See
  Melee:
    WORM EF 8 A_FaceTarget
    WORM G 8 A_SargAttack
    goto See
  Pain:
    WORM H 2
    WORM H 2 A_Pain
    goto See
  Death:
    WORM I 8
    WORM J 8 A_Scream
    WORM K 4
    WORM L 4 A_NoBlocking
    WORM M 4
    WORM N -1
    stop
  Raise:
    WORM NMLKJI 5
    goto See
  }
}

ACTOR DeadSlimeWorm
{
  States
  {
  Spawn:
    WORM N -1
    Stop
  }
}

actor SlimeBall1 : DoomImpBall
{
  radius 4
  height 4
  speed 14
  damage 4
  poisondamage 15
  seesound none
  deathsound "slimeball/splat"
  scale 0.5
  -NOGRAVITY
  states
  {
  Spawn:
    BOGY ABC 2 bright
    loop
  Death:
    BOGY D 0 bright A_NoGravity
    BOGY DEF 4 bright
    stop
  }
}

actor SlimeBall2 : SlimeBall1
{
speed 16
}

actor SlimeBall3 : SlimeBall1
{
speed 18
}

actor SlimeBall4 : SlimeBall1
{
speed 20
}

actor SlimeBall5 : SlimeBall1
{
speed 22
}

ACTOR SpiritImp 31179
{
  Health 150
  Radius 20
  Height 56
  Mass 120
  Speed 8
  BloodType None
  MONSTER
  RENDERSTYLE add
  Alpha 0.7
  +FloorClip
  +NoBloodDecals
  SEESOUND "SpiritImp/Sight"
  DEATHSOUND "SpiritImp/Death"
  ACTIVESOUND "SpiritImp/Active"
  HITOBITUARY "%o was chilled by the touch of a spirit imp"
  OBITUARY "%o was chilled by a spirit imp"  
  DropItem WarSpirit 64
  DropItem HBSpawner 24
  DropItem HBSpawner 24
  DropItem HBSpawner 24
  DropItem HBSpawner 24
  states
  {
  Spawn:
    SPIP AB 10 A_Look
    Loop
  See:
    SPIP A 0 A_CustomMissile("OrbitSpirit", 28, 0, 90)
    SPIP A 0 A_CustomMissile("OrbitSpirit2", 28, 0, -90)
    SPIP AABBCCDD 3 A_Chase
    Goto See+2
  Missile:
    SPIP EFG 4 A_FaceTarget 
    SPIP HIJK 5 A_FaceTarget
    SPIP L 6 A_CustomComboAttack ("SpiritImpBall", 28, 5, "Imp/Melee")
    SPIP M 10
    Goto See +2  
  Pain:
    SPIP N 3
    SPIP N 3 A_Pain
    Goto See +2
  Death:
    SPIP O 7 A_Scream
    SPIP PQR 7 
    SPIP S 6 A_CustomMissile("SpiritImpGhost1", 32, 0, 0) 
    SPIP T 5
    SPIP U 5 A_SpawnItem("SpiritImpGhost")
    SPIP V 5 A_NoBlocking
    SPIP W 3 A_FadeOut(0.015)
    Goto Death+8
    }
}
 
actor SpiritImpBall
{
    Radius 8
    Height 6
    Scale 0.4
    Speed 9
    Damage 2
    +SEEKERMISSILE
    PROJECTILE
    Renderstyle ADD
    SeeSound "harvester/scream"
    DeathSound "imp/shotx"
    Decal DoomImpScorch
    States
    {
    Spawn:
    	SIBA A  0 A_CustomMissile("SpiritImpBallTrail", 0, 0, 180)
        SIBA AB 2 A_SeekerMissile (10,20)
        Loop
    Death:
        SIBA CDEFGHIJKL 4 
        Stop
    }
}


actor SpiritImpBallTrail
{
    Radius 1
    Height 1
    Speed 0
    PROJECTILE
    RENDERSTYLE ADD
    ALPHA 0.5
    +NOCLIP
    States
    {
    Spawn:
    Death:
    	SITR H 6 
        SITR ABCDEFG 4 
        Stop
    }
}



actor OrbitSpirit : SpiritImpBall
{
     SeeSound ""
     +NOCLIP
	 +LowGravity
     Scale 0.4
     Alpha 0.1
     Speed 3
     MissileHeight 0
     States
     {
     Spawn:
         SIBA AABB 1 A_MissileAttack
         SIBA A 3 A_CustomMissile("OrbitSpirit", 0, 0, 75)
         SIBA A 0 A_MissileAttack
         Stop
     Death:
    	 SIBA A 2 A_FadeOut(0.015)
         loop
     }
}

actor OrbitSpirit2 : OrbitSpirit
{
     Speed 10
     MissileHeight 0
     States
     {
     Spawn:
         SIBA AABB 1 A_MissileAttack
         SIBA A 0 A_CustomMissile("OrbitSpirit", 0, 0, -75)
         SIBA A 0 A_MissileAttack
         stop
     Death:
    	 SIBA A 2 A_FadeOut(0.015)
         loop
     }
}


actor SpiritImpGhost1
{
    Radius 1
    Height 1
    Speed 0
    PROJECTILE
    RENDERSTYLE ADD
    ALPHA 0.7
    +NOCLIP
    SeeSound "harvester/ghost"
    States
    {
    Spawn:
    Death:
        SIGH ABCDEFG 4 
        Stop
    }
}

Actor SpiritImpGhost
{
  Radius 20
  Height 56
  Mass 120
  Speed 8
  renderstyle add
  alpha 0.2
  +NOTELEPORT
  +FLOORCLIP
  -SOLID
  -SHOOTABLE
  +NONSHOOTABLE
  +LOOKALLAROUND
  States
  {
  Spawn:
    SPIP AABB 6 A_Look
    SPIP A 0 A_FadeOut(0.005)
    Loop
  See:
    SPIP AABBCCDD 3 A_VileChase
    SPIP A 0 A_FadeOut(0.005)
    Loop
  Death:
    TNT1 A 1
    Stop
  Heal:
    TNT1 A 1
    Goto Death
  }
}

actor SoulHarvesterBall
{
    Radius 8
    Height 6
    Scale 0.4
    Speed 9
    Damage 2
    +SEEKERMISSILE
    PROJECTILE
    Renderstyle ADD
    SeeSound "harvester/scream"
    DeathSound "imp/shotx"
	Decal DoomImpScorch
    States
    {
    Spawn:
    	SHBA A  0 Bright A_CustomMissile("SoulBallTrail", 0, 0, 180)
        SHBA AB 2 Bright A_SeekerMissile (10,20)
        Loop
    Death:
        SHBA CDEFGHIJKL 4 Bright
        Stop
    }
}

actor SoulBallTrail
{
    Radius 1
    Height 1
    Speed 0
    PROJECTILE
    RENDERSTYLE ADD
    ALPHA 0.5
    +NOCLIP
    States
    {
    Spawn:
    Death:
    	SBTR H 6 
        SBTR ABCDEFG 4 Bright
        Stop
    }
}

ACTOR ScorpionMinion 30003
{
	Health 150
	Radius 20
	Height 56
	Speed 8
	PainChance 100
	MONSTER 
	Obituary "%o was zapped by a scorpian minion."
	ActiveSound "baby/active"
	DeathSound "baby/death"
	PainSound "baby/pain"
	SeeSound "baby/sight"
	DropItem MeatSpawner 100
	DropItem Cell
	DropItem Cell
	DropItem WarSpirit 28
	Scale 0.33
    States
    {
    Spawn:
        SCRP A 10 A_Look
        Loop
    See:
        SCRP BBCCDDEE 4 A_Chase
        Loop
    Missile:
		SCRP G 0 A_JUMP (120,"Missile2")
        SCRP A 10 A_FaceTarget
        SCRP G 1 A_FaceTarget
        SCRP G 0 A_PlaySound("ScorpLaser")
        SCRP F 2 BRIGHT A_CustomMissile("LaserFX", 30, 8, -4, 0)
        SCRP F 2
        SCRP F 2 BRIGHT A_CustomMissile("LaserFX2", 30, 8, 0, 0)
        SCRP F 2
        SCRP F 2 BRIGHT A_CustomMissile("LaserFX2", 30, 8, 4, 0)
        SCRP F 2      
        SCRP F 2 BRIGHT A_CustomMissile("LaserFX2", 30, 8, 8, 0)
        SCRP F 2
        SCRP F 2 BRIGHT A_CustomMissile("LaserFX2", 30, 8, 12, 0)
        SCRP F 2
        SCRP F 2 BRIGHT A_CustomMissile("LaserFX2", 30, 8, 16, 0)
        SCRP F 2
		SCRP G 4
        Goto See 
	Missile2:
        SCRP A 10 A_FaceTarget
        SCRP G 1 A_FaceTarget
        SCRP G 0 A_PlaySound("ScorpLaser")
        SCRP F 2 BRIGHT A_CustomMissile("LaserFX", 32, 8, 4, 0)
        SCRP F 2
        SCRP F 2 BRIGHT A_CustomMissile("LaserFX2", 32, 8, 0, 0)
        SCRP F 2
        SCRP F 2 BRIGHT A_CustomMissile("LaserFX2", 32, 8, -4, 0)
        SCRP F 2      
        SCRP F 2 BRIGHT A_CustomMissile("LaserFX2", 32, 8, -8, 0)
        SCRP F 2
        SCRP F 2 BRIGHT A_CustomMissile("LaserFX2", 32, 8, -12, 0)
        SCRP F 2
        SCRP F 2 BRIGHT A_CustomMissile("LaserFX2", 32, 8, -16, 0)
        SCRP F 2
		SCRP G 4
		Goto See
    Pain:
        SCRP H 8 A_Pain
        Goto See
    Death:
        SCRP H 4
        SCRP I 4 A_Scream
        SCRP J 4 A_NoBlocking
        SCRP K 4 A_SpawnItem("NashGore_GibGenerator")
        SCRP LM 4
        SCRP N -1
        Stop
    }
}

ACTOR DeadScorpionMinion
{
	Scale 0.33
	States
	{
	Spawn:
		SCRP N -1
		Stop
	}
}

ACTOR LaserFX
{
PROJECTILE
RenderStyle TRANSLUCENT
Alpha 0.5
Height 4
Radius 4
Speed 15
Damage 3
DeathSound "LaserHit"
States
{
Spawn:
LBFX A 12 BRIGHT
Loop
Death:
LBFX FGHI 3 BRIGHT
Stop
}
}

ACTOR LaserFX2
{
PROJECTILE
RenderStyle TRANSLUCENT
Alpha 0.5
Height 4
Radius 4
Speed 12
Damage 1
States
{
Spawn:
LBFX A 12 BRIGHT
Loop
Death:
LBFX FGHI 3 BRIGHT
Stop
}
}

ACTOR FrozenDemon 3700
{
  hitobituary "%o was bit by a Frozen demon."
  health 140
  painchance 10
  speed 10
  radius 30
  height 56
  mass 500
  Scale 1.15
  seesound "demon/sight"
  attacksound "demon/melee"
  painsound "demon/pain"
  deathsound "demon/death"
  activesound "demon/active"
  bloodcolor "33 33 33"
  MONSTER
  +NOBLOOD
  +FLOORCLIP
  +JUMPDOWN
  +LOOKALLAROUND
  DropItem Mana 180
  dropitem MeatSpawner 100
  DropItem WarSpirit 28
  damagefactor "ice",0
  states
  {
  Spawn:
  KIGL A 0 A_UnSetShootable
  KIGL AB 10 A_Look
  loop
  See:
  KIGL A 0 A_UnSetShootable
  KIGL A 0 A_UnSetSolid
  KIGL AABB 4 A_Chase
  Loop
  Melee:
    KIGL A 0 A_SetShootable
    KIGL A 0 A_SetSolid
    FARG EF 8 A_FaceTarget
    FARG E 0 A_PlayWeaponSound("DSFRZE")
    FARG F 0 A_CustomMissile("NitrogenNoDamage", 30, 10, 0)
    FARG F 2 Bright A_FaceTarget
    FARG G 0 A_CustomMissile("Nitrogen", 30, 10, 2)    
    FARG G 0 A_CustomMissile("Nitrogen", 30, 10, 2)    
    FARG G 0 A_CustomMissile("Nitrogen", 30, 10, -2)
    FARG G 0 A_CustomMissile("Nitrogen", 30, 10, -2)
    FARG G 2 Bright A_FaceTarget
    FARG G 0 A_CustomMissile("Nitrogen", 30, 10, -2)
    FARG G 0 A_CustomMissile("Nitrogen", 30, 10, -2)
    FARG G 0 A_CustomMissile("Nitrogen", 30, 10, 2)    
    FARG G 0 A_CustomMissile("Nitrogen", 30, 10, 2)    
	FARG ABCD 4 A_Chase("","",CHF_NORANDOMTURN)
    Goto See
  Missile:
    KIGL A 0 A_SetShootable
	FARG ABCD 4 A_Chase("","",CHF_NORANDOMTURN)
    FARG EF 8 A_FaceTarget
    FARG G 8 A_CustomMissile("IceBall",32,10,0)
    FARG G 8 A_CustomMissile("IceBall",32,-10,0)
    goto See
  Pain:
    FARG H 2
    FARG H 2 A_Pain
    goto See
  Death:
    FARG H 2 A_IceGuyDie
    stop
  Raise:
    FARG NMLKJI 5
    goto See
  }
}

ACTOR Nitrogen
{
    Speed 15
    Damage 3
    Projectile
    +RANDOMIZE
    +STRIFEDAMAGE
    Renderstyle Add
    Damagetype Ice
   Translation Ice
    Alpha 0.40
    States
    {
    Spawn:
        NBMC ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 Bright
        NBMC Z 0 A_Die
        Goto Death
    Death:
        NBMC Z 0 Bright
        Stop
    }
}

ACTOR NitrogenNoDamage : Nitrogen
{
    Damage 0
}

actor Dog 13301
{ 

    Health 30 
    Radius 20 
    Height 32
    Mass 50
    Speed 10 
    MeleeDamage 2
    MeleeRange 30
    PainChance 200 
    MONSTER 
    +FLOORCLIP 
    +NOTARGET
    DropItem MeatSpawner 50
	DropItem WarSpirit 28
	DropItem DemonicHeart 16
    Obituary "%o was mauled by a dog." 
    SeeSound "dog/active" 
    MeleeSound "dog/bark" 
    PainSound "dog/yelp" 
    DeathSound "dog/whine"
    ActiveSound "dog/active" 
	Scale 0.5
    States 
    { 
    Spawn: 
        DOGY AB 10 A_Look
        Loop 
    See: 
        DOGY AABBCCDD 2 A_Chase 
        Loop 
    Melee: 
        DOGY EF 4 A_FaceTarget 
        DOGY G 12 A_MeleeAttack 
        Goto See 
    Pain: 
        DOGY H 5 
        DOGY H 3 A_Pain 
        Goto See 
    Death: 
		DOGY I 8 A_Scream
    	DOGY J 7 A_NoBlocking
		DOGY K -1
    Raise: 
        DOGY KJI 5 
        Goto See 
    } 
} 

ACTOR DeadDog
{
	Scale 0.5
	States
	{
	Spawn:
		DOGY K -1
		Stop
	}
}

actor StealthDog : Dog
{ 
  +STEALTH
  RenderStyle Translucent
  Alpha 0
} 

Actor MiniEttin : Ettin
{
Scale 0.75
Radius 19
Height 51
+NOICEDEATH
SeeSound "ettin/sight"
MeleeSound "ettin/hit"
AttackSound "ettin/hit"
PainSound "ettin/pain"
DeathSound "ettin/death"
ActiveSound "ettin/active"
dropitem MeatSpawner 50
dropitem WarSpirit 28
  DropItem DemonicHeart 16
States
{
	 XDeath:		
		ETTB A 4
		ETTB B 4 A_NoBlocking
		ETTB C 4 A_SpawnItemEx("EttinMace", 0,0,8.5, random[DropMace](-128,127) * 0.03125, random[DropMace](-128,127) * 0.03125,
							   10 + random[DropMace](0,255) * 0.015625, 0, SXF_ABSOLUTEMOMENTUM)
		TNT1 A 0 A_SpawnItem("NashGore_GibGenerator")
		TNT1 A 0 A_SpawnItem("NashGore_GibGenerator")
		ETTB D 4 A_Scream
		ETTB E 4 A_QueueCorpse
		ETTB FGHIJK 4
		ETTB L -1
		Stop
	Ice:
	   NULL A 1 A_IceGuyDie
	   Stop
	  }
}

ACTOR DeadMiniEttin
{
	Scale 0.75
	States
	{
	Spawn:
		ETTN Q -1
		Stop
	}
}


Actor ShadowMiniEttin : MiniEttin
{
  +SHADOW
  RenderStyle OptFuzzy
  Alpha 0.5
}

ACTOR FlameDemon 21123
{              
	//$Category "ZDoom Monster pack"
   Health 150 
   PainChance 180 
   Speed 10 
   Radius 30 
   Height 56 
   Mass 400 
   Damage 0 
   Monster 
   +FLOORCLIP 
   SeeSound "demon/sight" 
   AttackSound "demon/melee" 
   PainSound "demon/pain" 
   DeathSound "demon/death" 
   ActiveSound "demon/active" 
   Obituary "%o got fired, REALLY FIRED." 
   dropitem MeatSpawner 100
   dropitem Mana 96
   States 
   { 
   Spawn: 
      MAUD AB 10 A_Look 
      Loop 
   See: 
      MAUD AABBCCDD 2 A_Chase 
      Loop 
   Melee:
	  MAUD EF 5 A_FaceTarget
	  MAUD E 0 A_PlaySound("dragonfam/attack")
	  MAUD E 2 A_CustomMissile("MiniFirePuff",32,0,0)
	  MAUD F 0 A_FaceTarget
	  MAUD E 2 A_CustomMissile("MiniFirePuff",32,0,0)
	  MAUD F 0 A_FaceTarget
	  MAUD F 2 A_CustomMissile("MiniFirePuff",32,0,0)
	  MAUD F 0 A_FaceTarget
	  MAUD G 2 A_CustomMissile("MiniFirePuff",32,0,0)
	  MAUD G 0 A_FaceTarget
	  MAUD G 0 A_SpidRefire
	  Goto Melee+1
   Missile: 
      MAUD E 4 A_FaceTarget 
      MAUD F 4 A_SkullAttack 
      Loop 
   Pain: 
      MAUD H 2 
      MAUD H 2 A_Pain 
      Goto See 
   Death: 
      MAUD I 8 
      MAUD J 8 A_Scream 
      MAUD K 4 
      MAUD L 4 A_NoBlocking 
      MAUD M 4 
      MAUD N -1 
      Stop 
   Raise: 
      MAUD N 5 
      MAUD MLKJI 5 
      Goto See 
   } 
} 

ACTOR DeadFlameDemon
{
	States
	{
	Spawn:
		MAUD N -1 
		Stop
	}
}

actor MiniFirePuff
{
    Radius 6
    Height 10
    Speed 7
    Damage 1
    PROJECTILE
    RENDERSTYLE ADD
    ALPHA 0.67
     Damagetype Fire
    States
    {
    Spawn:
        DFFP ABCDEFGH 2 Bright
    Death:
        DFFP IJKLMNOPQR 2 Bright
        Stop
    }
}

actor DarkSoul 8974
{
	Health 150
	Radius 22
	Height 62
	Mass 75
	Speed 12
	Painchance 40
	MONSTER
	+FLOORCLIP
	SeeSound "dsoul/sight"
	AttackSound "imp/attack"
	PainSound "dsoul/pain"
	DeathSound "dsoul/death"
	ActiveSound "dsoul/active"
	HitObituary "%o was blinded by a dark soul."
	Obituary "%o was cursed by a dark soul."
	MissileType DarkBall2
	DropItem MeatSpawner 110
	DropItem Mana 128
	DropItem WarSpirit
	States
	{
	Spawn:
	   DKSL AB 10 A_Look
	   Loop
	See:
	   DKSL ABCD 4 A_Chase
	   Loop
	Melee:
	Missile:
	   DKSL E 5 A_FaceTarget
	   DKSL F 5 Bright A_FaceTarget
	   DKSL E 5 A_FaceTarget
	   DKSL F 5 Bright A_FaceTarget
	   DKSL E 5 A_FaceTarget
	   DKSL F 5 Bright A_ComboAttack
	   Goto See
	Pain:
	   DKSL H 4
	   DKSL H 4 A_Pain
	   Goto See
	Death:
	   DKSL I 5
	   DKSL J 5 A_Scream
	   DKSL K 5 A_SpawnItemEx("DarkSoulSoul", 0,0,10, 0,0,1)
	   DKSL L 5
	   DKSL M 5 A_NoBlocking
	   DKSL NO 5 A_SetFloorClip
	   DKSL P -1
	   Stop

	}
}

actor DarkBall2 5996
{
	radius 23
  	height 19
  	speed 15
  	damage 5
  	renderstyle Add
  	deathsound "ball3/shotx"
  	PROJECTILE
  	+RANDOMIZE
  	+SEEKERMISSILE
  	+SHOOTABLE
  	states
  	{
  	Spawn:
    		BLL3 A 0 A_SetShootable
                BLL3 A 5 A_SeekerMissile(30,30) 
		BLL3 A 0 A_Tracer
    		loop
  	Death:  
    		BLL3 BCDEF 5 bright
    		stop
  	}
}

actor DarkSoulSoul 8221
{
  +NOBLOCKMAP
  +NOGRAVITY
  states
  {
  Spawn:
    DSOL QRS 5
    DSOL T 9
    DSOL UVW 5
    stop
  }
}

actor DarkSoulGhost : DarkSoul 6774
{
	+SHADOW
	+GHOST
	RenderStyle Translucent
	Alpha 0.5
}

Actor D3Wraith
{
  hitobituary "%o was slashed by a Wraith"
  health 190
  radius 22
  height 56
  mass 200
  speed 12
  painchance 64
  meleedamage 4
  seesound "D3Wraith/Sight"
  painsound "D3Wraith/Pain"
  deathsound "D3Wraith/Death"
  activesound "D3Wraith/Active"
  meleesound "D3Wraith/Hit"
  dropitem MeatSpawner 100
  dropitem Mana 96
  dropitem WarSpirit 98
  MONSTER
  +FLOORCLIP
  states
  {
  Spawn:
    3WRT A 1
    3WRT A 0 A_JumpIf(Args[0] > 0, "Spawn2")
    3WRT A 0 A_JumpIfInventory("WraithTeleporting", 1, "StopTeleport")
    3WRT AAAAAAAAAA 1 A_Look
    3WRT AAAAAAAAAA 1 A_Look
    3WRT AAAAAAAAAA 1 A_Look
    3WRT AAAAAAAAAA 1 A_Look
    3WRT AAAAAAAAAA 1 A_Look
    3WRT A 0 A_JumpIf(Args[2] > 0, 2)
    3WRT A 0 A_Jump(16, 2)
    3WRT A 0
    Goto Spawn+1
    3WRT A 0 A_PlaySound("D3Wraith/Idle")
    Goto Spawn+1
  Spawn2:
    3WRT A 0 A_JumpIfInventory("WraithTeleporting", 1, "StopTeleport")
    3WRT AAAAAAAAAA 1 A_LookEx(8, 0, 0, 0, 0, "Teleport")
    3WRT AAAAAAAAAA 1 A_LookEx(8, 0, 0, 0, 0, "Teleport")
    3WRT AAAAAAAAAA 1 A_LookEx(8, 0, 0, 0, 0, "Teleport")
    3WRT AAAAAAAAAA 1 A_LookEx(8, 0, 0, 0, 0, "Teleport")
    3WRT AAAAAAAAAA 1 A_LookEx(8, 0, 0, 0, 0, "Teleport")
    3WRT A 0 A_JumpIf(Args[2] > 0, 2)
    3WRT A 0 A_Jump(16, 2)
    3WRT A 0
    Loop
    3WRT A 0 A_PlaySound("D3Wraith/Idle")
    Loop
  StopTeleport:
    3WRT A 0 A_SpawnItem("TeleportFog")
    3WRT A 0 A_ChangeFlag("Invulnerable", 0)
    3WRT A 0 A_SetShootable
    3WRT A 0 A_ChangeFlag("NonShootable", 0)
    3WRT A 0 A_ChangeFlag("Shootable", 1)
    3WRT A 0 A_ChangeFlag("NoTeleport", 0)
    3WRT A 0 A_ChangeFlag("NoRadiusDMG", 0)
    3WRT A 0 A_TakeInventory("WraithTeleporting", 1)
    Goto Spawn
  See:
    3WRT B 2 A_Chase("", "", 0)
    3WRT BBCCCDDDEEE 2 A_Chase
    3WRT B 0 A_JumpIf(Args[1] > 0, 1)
    Goto See+1
    3WRT B 0 A_Jump(16, "Missile")
    Goto See+1
  Melee:
    3WRT A 0 A_JumpIfInventory("WraithTeleporting", 1, 5)
    3WRT A 6 A_FaceTarget
    3WRT F 5 A_FaceTarget
    3WRT G 0 A_PlayWeaponSound("D3Wraith/Attack")
    3WRT G 8 A_MeleeAttack
    goto see
    3WRT A 0 A_SpawnItem("TeleportFog")
    3WRT A 0 A_ChangeFlag("Invulnerable", 0)
    3WRT A 0 A_SetShootable
    3WRT A 0 A_ChangeFlag("NonShootable", 0)
    3WRT A 0 A_ChangeFlag("Shootable", 1)
    3WRT A 0 A_ChangeFlag("NoTeleport", 0)
    3WRT A 0 A_ChangeFlag("NoRadiusDMG", 0)
    3WRT A 0 A_ChangeFlag("DropOff", 0)
    3WRT A 0 A_TakeInventory("WraithTeleporting", 1)
    3WRT F 5 A_FaceTarget
    3WRT G 0 A_PlayWeaponSound("D3Wraith/Attack")
    3WRT G 8 A_MeleeAttack
    Goto See
  Missile:
    3WRT A 0 A_JumpIfCloser(128, 2)
    3WRT A 0 A_JumpIfCloser(1024, 2)
    3WRT A 0
    Goto See
    3WRT A 0 A_Jump(128, "Teleport")
    Goto See
  Teleport:
    3WRT AAA 6 A_FaceTarget
    TNT1 A 0 A_GiveInventory("WraithTeleporting", 1)
    TNT1 A 0 A_SpawnItem("TeleportFog")
    TNT1 A 0 A_ChangeFlag("Invulnerable", 1)
    TNT1 A 0 A_ChangeFlag("NonShootable", 1)
    TNT1 A 0 A_ChangeFlag("NoTeleport", 1)
    TNT1 A 0 A_ChangeFlag("NoRadiusDMG", 1)
    TNT1 A 0 A_ChangeFlag("DropOff", 1)
    TNT1 A 0 A_UnSetShootable
    TNT1 AAAAAAAAAAAAAAAAAAAAAAAAA 0 A_ExtChase(1, 0, 0, 0)
    TNT1 AAAAAAAAAAAAAAAAAAAAAAAAA 0 A_ExtChase(1, 0, 0, 0)
    TNT1 A 1 A_ExtChase(1, 0, 0, 0)
    3WRT A 0 A_SpawnItem("TeleportFog")
    3WRT A 0 A_ChangeFlag("Invulnerable", 0)
    3WRT A 0 A_SetShootable
    3WRT A 0 A_ChangeFlag("NonShootable", 0)
    3WRT A 0 A_ChangeFlag("Shootable", 1)
    3WRT A 0 A_ChangeFlag("NoTeleport", 0)
    3WRT A 0 A_ChangeFlag("NoRadiusDMG", 0)
    3WRT A 0 A_ChangeFlag("DropOff", 0)
    3WRT A 0 A_TakeInventory("WraithTeleporting", 1)
    3WRT AA 6 A_FaceTarget
    Goto See
  Pain:
    3WRT A 2
    3WRT A 2 A_Pain
    Goto See
  Death:
    3WRT H 8
    3WRT I 8 A_Scream
    3WRT J 6
    3WRT K 6 A_NoBlocking
    3WRT L -1
    Stop
  Raise:
    TROO LKJIH 8
    Goto See
  }
}

ACTOR DeadWraith
{
	States
	{
	Spawn:
		3WRT L -1
		Stop
	}
}

Actor WraithTeleporting : Inventory
{
Inventory.MaxAmount 1
}

ACTOR BerserkMarine
{
	Health 100
	Radius 20
	Height 56
	Speed 15
	PainChance 200
	MONSTER 
	+FLOORCLIP
	AttackSound "human/fist"
    activesound "misc/combineidle"
    seesound "misc/combineidle"
	PainSound "misc/combinepain"
	DeathSound "misc/combinedeath"
	MeleeDamage 20
	MeleeSound "*fist"
	HitObituary "%o got %h sucker punched by a marine"
    DropItem PortablePack 100
	DropItem WarSpirit 56
	DropItem HealthVile 210
	DropItem NewBerserk 125
    TRANSLATION 2
	States
	{
	Spawn:
		PLA1 A 10 A_Look
		Loop
	See:
		PLA1 A 0 A_ClearTarget
		PLA1 A 0 A_Chase
		PLA1 A 2 A_Chase
		PLA1 A 2 A_Recoil (-1)
		PLA1 B 0 A_Chase
		PLA1 B 2 A_Chase
		PLA1 B 2 A_Recoil (-1)
		PLA1 C 0 A_Chase
		PLA1 C 2 A_Chase
		PLA1 C 2 A_Recoil (-1)
		PLA1 D 0 A_Chase
		PLA1 D 2 A_Chase
		PLA1 D 2 A_Recoil (-1)
		Loop		
    Melee:
        PLA1 A 4 A_FaceTarget
        PLA1 E 2 A_MeleeAttack
        PLA1 F 4 A_FaceTarget
        Goto See
	Pain:
		PLA1 G 0 A_ExtChase (0, 0)
		PLA1 G 0 A_ExtChase (0, 0)
		PLA1 G 2 A_Recoil (1.3)
		PLA1 G 0 A_ExtChase (0, 0)
		PLA1 G 0 A_ExtChase (0, 0)
		PLA1 G 0 A_Pain
		PLA1 G 2 A_Recoil (1.3)
		PLA1 G 0 A_ExtChase (0, 0)
		PLA1 G 0 A_ExtChase (0, 0)
		PLA1 G 2 A_Recoil (1.3)
		Goto See
	Death:
		PLA1 H 5
		PLA1 I 5 A_Scream
		PLA1 J 5 A_NoBlocking
		PLA1 K 5
		PLA1 L 5
		PLA1 M 5
		PLA1 N -1
		Stop
	XDeath:
		PLA1 O 5
		PLA1 P 5 A_XScream
        TNT1 A 0 A_SpawnItem("NashGore_GibGenerator")
        TNT1 A 0 A_SpawnItem("NashGore_GibGenerator")
		PLA1 Q 5 A_NoBlocking
		PLA1 RSTUV 5
		PLA1 W -1
		Stop
    Burn:                
        APBX A 1 A_HideThing
        BCUL I 0 A_NoBlocking
        BCUL I 0 A_SpawnItem("BurnDeath",1,0,0)
        APBX A -1
        Stop
	Raise:
		PLA1 NMLKJIH 5
		Goto See
	}
}

ACTOR DeadBerserkMarine
{
	Translation 2
	States
	{
	Spawn:
		PLA1 N -1
		Stop
	}
}

actor Berserker
{
    Health 150
    Radius 20
    Height 64
    Scale 0.46
    Speed 11
    PainChance 150
    Mass 400
    MONSTER 
    +FLOORCLIP
    SeeSound "q2berserker/sight"
    PainSound "q2berserker/pain"
    DeathSound "q2berserker/death"
    ActiveSound "q2berserker/idle"
    MeleeSound "implord/macehit"
    Obituary "%o was smashed by a berserker."
    MeleeDamage 7
    dropitem PortablePack 88
    dropitem MeatSpawner 90
    DropItem WarSpirit 48
    States
    {
    Spawn:
        BERI A 10 A_Look
	Loop
    See:
        BERW AABBCCDDEEFF 2 A_Chase
	Loop
    Melee:
	  NULL A 0 A_Jump(128,6)
	  NULL A 0 A_PlaySound("q2berserker/attack")
        BERA AB 5 A_FaceTarget
        BERA C 5 A_MeleeAttack
	  BERA D 5
        Goto See
	  NULL A 0 A_PlaySound("q2berserker/attack")
        BERA EFG 5 A_FaceTarget
        BERA H 5 A_MeleeAttack
	  BERA I 5
        Goto See
    Pain:
        BERP A 2
        BERP A 5 A_Pain
        Goto See
    Death:
        BERD A 4
        BERD B 4 A_Scream
        BERD C 4
        BERD D 4 A_NoBlocking
        BERD EFG 4
        BERD H -1
        Stop
    Raise:
        BERD HGFEDCBA 6
        Goto See
    }
}

ACTOR DeadBerserker
{
    Scale 0.46
	States
	{
	Spawn:
		BERD H -1
		Stop
	}
}

Actor StealthBerserker : Berserker 
{
  +STEALTH
  RenderStyle Translucent
  Alpha 0
}

actor Tenaj 22111
{
  obituary "%o got burnt by a Tenaj"
  health 150
  radius 20
  height 56
  mass 100
  speed 6
  scale 0.8
  painchance 200
  seesound "Ten/sight"
  painsound "Ten/pain"
  deathsound "Ten/death"
  activesound "Ten/active"
  BloodColor Blue
  Damagefactor "Fire", 0.4
  dropitem MeatSpawner 50
  DropItem Fuel
  DropItem Portablepack 64  
  DropItem Fuel 128
  DropItem Fuel 128
  DropItem WarSpirit 32
  MONSTER
  +FLOORCLIP
  states
  {
  Spawn:
    TENN AB 10 A_Look
    loop
  See:
    TENN AABBCCDD 3 A_Chase
    loop
  Missile:
    TENN E 0 A_PlaySound("vile/firecrkl")
    TENN E 0 A_Jump(208,7)
    TENN E 2 Bright A_FaceTarget
    TENN E 0 A_CustomMissile("ZombieFlame", 30, 10, 0)
    TENN E 2 Bright A_FaceTarget
    TENN E 0 A_CustomMissile("ZombieFlame", 30, 10, 2)    
    TENN F 2 Bright A_FaceTarget
    TENN F 0 A_CustomMissile("ZombieFlame", 30, 10, -2)
    Goto See
    TENN E 2 Bright A_FaceTarget
    TENN E 0 A_CustomMissile("ZombieFlame", 30, 10, 0)
    TENN E 2 Bright A_FaceTarget
    TENN E 0 A_CustomMissile("ZombieFlame", 30, 10, 2)
    TENN F 2 Bright A_FaceTarget
    TENN F 0 A_CustomMissile("ZombieFlame", 30, 10, -2)
    Goto Missile+1
  Pain:
    TENN H 2
    TENN H 2 A_Pain
    goto See
  Death:
    TENN I 8
    TENN J 8 A_Scream
    TENN K 6
    TENN L 6 A_NoBlocking
    TENN M -1
    stop
  Raise:
    TENN LKJI 6
    goto See
  Burn:                
    APBX A 1 A_HideThing
    BCUL I 0 A_NoBlocking
    BCUL I 0 A_SpawnItem("BurnDeath",1,0,0)
    APBX A -1
    Stop
  }
}

ACTOR DeadTenaj

{
  scale 0.8
  States
  {
  Spawn:
    TENN M -1
    Stop
  }
}

actor FlameProjectile
{
	Radius 12
	Height 8
	Speed 15
	Damage 2
	Scale 0.2
	Projectile
    Damagetype Fire
	DontHurtShooter
	ExplosionDamage 24
	ExplosionRadius 32
	States
	{
	Spawn:
		FIRE E 2 Bright A_Explode
		FIRE E 2 Bright A_Explode
		FIRE E 2 Bright A_Explode
		FIRE E 2 Bright A_Explode
		FIRE E 2 Bright A_Explode
		FIRE F 2 Bright A_Explode
		FIRE F 2 Bright A_Explode
		FIRE F 2 Bright A_Explode
		FIRE F 2 Bright A_Explode
		FIRE F 2 Bright A_Explode
		FIRE G 2 Bright A_Explode
		FIRE G 2 Bright A_Explode
		FIRE G 2 Bright A_Explode
		FIRE G 2 Bright A_Explode
		FIRE G 2 Bright A_Explode
		Stop
	}
}

actor ZombieFlame : FlameProjectile
{
	Damage 1
	ExplosionDamage 6
}